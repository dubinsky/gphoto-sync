plugins {
  id 'com.github.ben-manes.versions' version '0.53.0'
  id 'scala'
  id 'java-library'
}

description = 'Process Photographs'
group = 'org.podval.tools'
version = '0.0.1'

java.toolchain.languageVersion = JavaLanguageVersion.of(21)
scala.scalaVersion = '3.7.4'
project.gradle.startParameter.excludedTaskNames.add('compileJava')

dependencies {
  implementation 'org.slf4j:slf4j-api:2.0.17'
  implementation 'com.drewnoakes:metadata-extractor:2.19.0'
}

def isNonStable = { String version ->
  boolean stableKeyword = ['RELEASE', 'FINAL', 'GA'].any { it -> version.toUpperCase().contains(it) }
  String regex = /^[0-9,.v-]+(-r)?$/
  return !stableKeyword && !(version ==~ regex)
}

tasks.named("dependencyUpdates").configure {
  rejectVersionIf {
    isNonStable(it.candidate.version)
  }
}

tasks.withType(ScalaCompile).configureEach {
  scalaCompileOptions.with {
    additionalParameters = [
      '-new-syntax',
      '-feature',
      '-language:strictEquality',
      '-source:future'
    ]
  }
}

jar {
  manifest {
    attributes(
      'Implementation-Title'  : project.description,
      'Implementation-Version': project.version
    )
  }
}

tasks.register('sourcesJar', Jar) {
  from sourceSets.main.allSource
  archiveClassifier.set('sources')
}

tasks.register('scaladocJar', Jar) {
  from scaladoc.destinationDir
  archiveClassifier.set('scaladoc')
}
scaladocJar.dependsOn scaladoc
